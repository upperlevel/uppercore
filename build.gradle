import org.apache.tools.ant.filters.*

plugins {
    id "java"
    id "maven"
    id "com.github.johnrengelman.shadow" version "2.0.2"
    // id "checkstyle"
}

group = "xyz.upperlevel.uppercore"
version = "2.0"
description = "Global API for Spigot plugins"

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext {
    mcVersion = "1.12-R0.1-SNAPSHOT"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/" // maven snapshots
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" // bukkit
    }
    maven {
        url "http://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url "http://nexus.hc.to/content/repositories/pub_releases" // vault
    }
    maven {
        url "http://repo.bstats.org/content/repositories/releases/" // metrics
    }
    maven {
        url "http://maven.elmakers.com/repository/" // EffectLib
    }
}

dependencies {
    /* Lombok */
    compileOnly "org.projectlombok:lombok:1.18.2"
    testCompileOnly "org.projectlombok:lombok:1.18.2"
    integrationTestCompileOnly "org.projectlombok:lombok:1.18.2"

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.+"

    compileOnly "org.spigotmc:spigot-api:" + mcVersion
    testCompile "org.spigotmc:spigot-api:" + mcVersion
    integrationTestCompileOnly "org.spigotmc:spigot-api:" + mcVersion

    compileOnly "org.bukkit:bukkit:" + mcVersion
    testCompile "org.bukkit:bukkit:" + mcVersion
    integrationTestCompileOnly "org.spigotmc:spigot-api:" + mcVersion

    compile "xyz.upperlevel.spigot.anvilgui:spigot-anvil-gui:1.3" // anvil-gui
    compile "de.slikey:EffectLib:6.1" // EffectLib
    compileOnly "me.clip:placeholderapi:2.9.2" // placeholder-api
    integrationTestCompileOnly "me.clip:placeholderapi:2.9.2" // placeholder-api
    compileOnly "net.milkbowl.vault:VaultAPI:1.6" // vault

    compile "org.bstats:bstats-bukkit:1.1" // metrics

    /* Db */
    compileOnly "org.mongodb:mongo-java-driver:3.8.0"
    compileOnly "com.rethinkdb:rethinkdb-driver:2.3.3"
    compileOnly "org.dizitart:nitrite:3.0.2"
}

shadowJar {
    relocate "org.bstats", "${project.group}.libs.metrics"
    baseName = "Uppercore"
    classifier = null
    version = null
}
project.tasks.assemble.dependsOn project.tasks.shadowJar

/*
checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    configProperties = [
            'configDir': rootProject.file('config/checkstyle'),
            'baseDir': rootDir,
    ]
    toolVersion = '7.3'
    ignoreFailures = true
}
*/

task buildTest(type: Jar) {
    from sourceSets.integrationTest.output
    baseName = "UppercoreTest"
}

processResources {
    filter(ReplaceTokens, tokens: ["project.version": project.property("version")])
}
