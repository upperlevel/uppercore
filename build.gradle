import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "java"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version "5.1.0"
    // id "checkstyle"
}

group = "xyz.upperlevel.uppercore"
version = "2.0"
description = "Global API for Spigot plugins"

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext {
    mcVersion = "1.15.2-R0.1-SNAPSHOT"
}

test {
    testLogging {
        exceptionFormat = "full"
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/" // maven snapshots
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" // bukkit
    }
    maven {
        url "https://mvnrepository.com/artifact/net.milkbowl.vault/VaultAPI" // vault
    }
    maven {
        url "https://mvnrepository.com/artifact/org.bstats/bstats-bukkit" // bstats
    }
    maven {
        url "http://maven.elmakers.com/repository/" // EffectLib
    }
    maven {
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/" // PlaceholderAPI
    }
}

dependencies {
    /* Lombok */
    compileOnly "org.projectlombok:lombok:1.18.8"
    annotationProcessor "org.projectlombok:lombok:1.18.8"

    testCompileOnly "org.projectlombok:lombok:1.18.8"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.8"

    integrationTestCompileOnly "org.projectlombok:lombok:1.18.8"
    integrationTestAnnotationProcessor "org.projectlombok:lombok:1.18.8"

    /* JUnit */
    testCompile "junit:junit:4.12"

    /* Mockito */
    testCompile "org.mockito:mockito-core:2.+"

    /* Spigot API */
    compileOnly "org.spigotmc:spigot-api:" + mcVersion
    testCompile "org.spigotmc:spigot-api:" + mcVersion
    integrationTestCompileOnly "org.spigotmc:spigot-api:" + mcVersion

    /* Bukkit */
    compileOnly "org.bukkit:bukkit:" + mcVersion
    testCompile "org.bukkit:bukkit:" + mcVersion
    integrationTestCompileOnly "org.bukkit:bukkit:" + mcVersion

    compile "xyz.upperlevel.spigot.anvilgui:spigot-anvil-gui:1.3" // anvil-gui
    compileOnly "me.clip:placeholderapi:2.9.2" // placeholder-api
    integrationTestCompileOnly "me.clip:placeholderapi:2.9.2" // placeholder-api
    compileOnly "net.milkbowl.vault:VaultAPI:1.6" // vault

    compile "org.bstats:bstats-bukkit:1.1" // metrics

    /* Db */
    compileOnly "org.mongodb:mongo-java-driver:3.8.0"
    compileOnly "com.rethinkdb:rethinkdb-driver:2.3.3"
    compileOnly "org.dizitart:nitrite:3.0.2"
}

shadowJar {
    classifier = null
    
    relocate "org.bstats", "${project.group}.libs.metrics"
}

task testJar(type: ShadowJar) {
    classifier = "test"

    dependsOn tasks.shadowJar

    from zipTree(shadowJar.outputs.files.singleFile)
    from sourceSets.integrationTest.output
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    /*repositories {
        maven {
            url "http://repo.myorg.com"
        }
    }*/
}

/*
checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    configProperties = [
            'configDir': rootProject.file('config/checkstyle'),
            'baseDir': rootDir,
    ]
    toolVersion = '7.3'
    ignoreFailures = true
}
*/

// This part of the config is needed to filter the class UppercoreInfo.java, replacing the needed properties

/*
task generateUppercoreInfo(type: Copy) {
    println(buildDir)
    from ("${projectDir}/src/main/java/xyz/upperlevel/uppercore") {
        include "UppercoreInfo.java"

        filter { line -> line.replaceAll('@project.version@', (String)project.property("version")) }
    }
    into "$buildDir/generated/sources/custom"
}

sourceSets {
    main {
        java {
            srcDirs ("${projectDir}/src/main/java", "${buildDir}/generated/sources/custom")
            exclude("xyz/upperlevel/uppercore/UppercoreInfo.java")
        }
    }
}

compileJava {
    dependsOn generateUppercoreInfo
}*/