import org.apache.tools.ant.filters.*

plugins {
    id "java"
    id "maven"
    id "com.github.johnrengelman.shadow" version "2.0.2"
}

group = "xyz.upperlevel.uppercore"
version = "1.3"
description = "Global API for Spigot plugins"

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext {
    mcVersion = "1.12-R0.1-SNAPSHOT"
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources {
            srcDir file('src/integration-test/resources')
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/" // maven snapshots
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" // bukkit
    }
    maven {
        url "http://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url "http://nexus.hc.to/content/repositories/pub_releases" // vault
    }
    maven {
        url "http://repo.bstats.org/content/repositories/releases/" // metrics
    }
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.16.20"
    testCompileOnly "org.projectlombok:lombok:1.16.20"
    integrationTestCompileOnly "org.projectlombok:lombok:1.16.20"

    testCompile "junit:junit:4.12"

    compileOnly "org.spigotmc:spigot-api:" + mcVersion
    testCompileOnly "org.spigotmc:spigot-api:" + mcVersion
    integrationTestCompileOnly "org.spigotmc:spigot-api:" + mcVersion

    compileOnly "org.bukkit:bukkit:" + mcVersion
    testCompileOnly "org.bukkit:bukkit:" + mcVersion
    integrationTestCompileOnly "org.spigotmc:spigot-api:" + mcVersion

    compile "xyz.upperlevel.spigot.anvilgui:spigot-anvil-gui:1.3" // anvil-gui
    compileOnly "me.clip:placeholderapi:2.0.8" // placeholder-api
    compileOnly "net.milkbowl.vault:VaultAPI:1.6" // vault

    compile "org.bstats:bstats-bukkit:1.1" // metrics

    compileOnly "org.mongodb:mongo-java-driver:3.5.0"
    compileOnly "com.rethinkdb:rethinkdb-driver:2.3.3"
    compileOnly "org.dizitart:nitrite:2.1.1"
}

shadowJar {
    relocate "org.bstats", "${project.group}.libs.metrics"
    baseName = "Uppercore"
    classifier = null
    version = null
}
project.tasks.assemble.dependsOn project.tasks.shadowJar

task buildTest(type: Jar) {
    from sourceSets.integrationTest.output
    baseName = "UppercoreTest"
}

processResources {
    filter(ReplaceTokens, tokens: ["project.version": project.property("version")])
}
